#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

m_img_translate = function(Matrix[Double] img_in, Double offset_x, Double offset_y, Integer out_w, Integer out_h, Double fill_value) return (Matrix[Double] img_out) {
  # Translates by x pixels to the right and y pixels down

  w = ncol(img_in)
  h = nrow(img_in)
  offset_x = round(offset_x)
  offset_y = round(offset_y)
  start_x = 1 - offset_x
  start_y = 1 - offset_y
  end_x = max(w , out_w) - offset_x
  end_y = max(h, out_h) - offset_y
  if (start_x < 1)
    start_x = 1
  if (start_y < 1)
    start_y = 1
  if (w < end_x)
    end_x = w
  if (out_w < end_x + offset_x)
    end_x = out_w - offset_x
  if (h < end_y)
    end_y = h
  if (out_h < end_y + offset_y)
    end_y = out_h - offset_y

  img_out = matrix(fill_value, rows=out_h, cols=out_w)
  if (start_x < end_x & start_y < end_y) {
    img_out[(start_y+offset_y):(end_y+offset_y), (start_x+offset_x):(end_x+offset_x)] = img_in[start_y:end_y, start_x:end_x]
  }
}
